// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Memory.init();
        do Math.init();
        do Screen.init();
        do Output.init();
        do Keyboard.init();

        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        // Uses an infinite loop to make it seem program has stopped
        while (true) {}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int innerCounter;
        var int outerCounter;
        let innerCounter = 0;
        let outerCounter = 0;
        while (~(outerCounter = 100)) {
            while (~(innerCounter = duration)) {
                let innerCounter = innerCounter + 1;
            }
            let innerCounter = 0; // resets for next loop
            let outerCounter = outerCounter + 1;
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Output.printString("ERR<");
        do Output.printInt(errorCode);
        do Output.printString(">");
        do Sys.halt();
        return;
    }
}
